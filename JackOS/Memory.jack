/**
 * This library provides two services: direct access to the computer's main
 * memory (RAM), and allocation and recycling of memory blocks. The Hack RAM
 * consists of 32,768 words, each holding a 16-bit binary number.
 */ 
class Memory {
    static int freeMemoryAddress;
    static int ramBase;
    static int ramMax;
    static Array memoryCells;

    function void init() 
    {
        let memoryCells = 0;
        let freeMemoryAddress = 0;
        let ramBase = 2048;    
        let ramMax = 16383;
        return;
    }

    function int peek(int address) 
    {
        return memoryCells[address];
    }

    function void poke(int address, int value) 
    {
        let memoryCells[address] = value;
        return;
    }

    function Array alloc(int size) 
    {
        var int smallestAddress;
        var int previousAddress;
        var int beforeSmallestAddress;
        var int currentAddress;
        var int currentSize;
        var int allocatedAddress;

        var int leftOverSize;
        var int allocatedSize;
        var int newBlockAddress;

        if (size < 0)
        {
            // do Sys.Error("Incorrect size");
            return -1;
        }

        if (freeMemoryAddress = 0)
        {
            return Memory.allocFromNotUsedRam(size);
        }

        let smallestAddress = 0;
        let previousAddress = 0;
        let currentAddress = freeMemoryAddress;
        
        while (currentAddress > 0)
        {
            let currentSize = Memory.peek(currentAddress + 1);
            if (~(currentSize < size))
            {
                if (smallestAddress = 0)
                {
                    let smallestAddress = currentAddress;
                    let beforeSmallestAddress = previousAddress;
                }
                else 
                {
                    if (currentSize < Memory.peek(smallestAddress + 1))
                    {
                        let smallestAddress = currentAddress;
                        let beforeSmallestAddress = previousAddress;
                    }
                }
            }

            let previousAddress = currentAddress;
            // by addr[currentAddress] there is value of next address
            let currentAddress = Memory.peek(currentAddress); 
        }

        if (smallestAddress = 0 )
        {
            return Memory.allocFromNotUsedRam(size);
        }

        // need to relink memory blocks with each other and then - return free
        let allocatedSize = size;
        let leftOverSize = Memory.peek(smallestAddress + 1) - size - 2;

        if (leftOverSize < 1)
        {
            let allocatedSize = Memory.peek(smallestAddress + 1);
            if (beforeSmallestAddress = 0)
            {
                let freeMemoryAddress = smallestAddress; // it was first memory block
            }
            else 
            {
                do Memory.poke(beforeSmallestAddress, smallestAddress);
            }
        }
        else
        {
            // need to create a new block instead of old, smaller one. 
            // then connect previous one to it. 
            // then connect new one to the next 

            let newBlockAddress = smallestAddress + 2 + allocatedSize;
            do Memory.poke(newBlockAddress + 1, leftOverSize); 

            if (beforeSmallestAddress = 0)
            {
                let freeMemoryAddress = smallestAddress;
            }
            else
            {
                do Memory.poke(beforeSmallestAddress, newBlockAddress);
            }

            do Memory.poke(newBlockAddress, Memory.peek(smallestAddress));
        }

        if(Memory.peek(smallestAddress) = 0)
        {
            let freeMemoryAddress = 0;
        }

        do Memory.poke(smallestAddress+1, allocatedSize);

        return newBlockAddress;
    }

     /** De-allocates the given object (cast as an array) by making
     *  it available for future allocations. */
    function void deAlloc(Array o) 
    {
        var int curAddress;
        var int prevAddress;
         
        if(freeMemoryAddress = 0)
        {
            let freeMemoryAddress = o-2;
            do Memory.poke(o-2, 0); // clear previous link
            return;
        }

        let curAddress = freeMemoryAddress;
        let prevAddress = 0;

        // simple defragmentation
        while(curAddress > 0)
        {
            if(o + Memory.peek(o-1) = curAddress)
            {
                if(prevAddress = 0)
                {
                   let freeMemoryAddress = o - 2;
                }
                else
                {
                    do Memory.poke(prevAddress, o-2);
                }

                do Memory.poke(o-1, Memory.peek(o-1) + Memory.peek(curAddress + 1) + 2);     
            }
            else 
            {
                do Memory.poke(curAddress + 1, Memory.peek(curAddress+1) + 2 + Memory.peek(o-1));
            }

            let prevAddress = curAddress;
            let curAddress = Memory.peek(curAddress);
        }

        do Memory.poke(curAddress, o-2);
        do Memory.poke(o-2, 0); // clear previous link

        return;
    }    

    function int allocFromNotUsedRam(int size)
    {
       var int allocatedAddr;
        if(ramBase + size > ramMax - 2)
        {
            // do Sys.Error("No memory");
            return -1;
        }

        let allocatedAddr = ramBase;
        do Memory.poke(ramBase, 0);
        do Memory.poke(ramBase + 1, size);

        let ramBase = ramBase + 2 + size;
            
        return allocatedAddr + 2;     
    }
}
